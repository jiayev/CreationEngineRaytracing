#include "raytracing/include/Common.hlsli"
#include "raytracing/include/Registers.hlsli"
#include "raytracing/include/Payload.hlsli"
#include "raytracing/include/Geometry.hlsli"

[shader("raygeneration")]
void Main()
{
    uint2 idx = DispatchRaysIndex().xy;
    uint2 size = DispatchRaysDimensions().xy;
    
    RayDesc ray = SetupPrimaryRay(idx, size);
    
    Payload payload;
    payload.hitDistance = -1.0f;
    payload.primitiveIndex = 0;
    payload.PackBarycentrics(float2(0.0f, 0.0f));
    payload.PackInstanceGeometryIndex(0, 0);
    payload.randomSeed = 0;   
    
#if USE_RAY_QUERY
    RayQuery<RAY_FLAG_NONE> rayQuery;
    rayQuery.TraceRayInline(Scene, RAY_FLAG_NONE, 0xFF, ray);

    /*while (rayQuery.Proceed())
    {
        if (rayQuery.CandidateType() == CANDIDATE_NON_OPAQUE_TRIANGLE)
        {
            if (considerTransparentMaterial(
                rayQuery.CandidateInstanceID(),
                rayQuery.CandidatePrimitiveIndex(),
                rayQuery.CandidateGeometryIndex(),
                rayQuery.CandidateTriangleBarycentrics()))
            {
                rayQuery.CommitNonOpaqueTriangleHit();
            }
        }
    }*/

    if (rayQuery.CommittedStatus() == COMMITTED_TRIANGLE_HIT)
    {
        payload.hitDistance = rayQuery.CommittedRayT();
        payload.primitiveIndex = rayQuery.CommittedPrimitiveIndex();
        payload.PackBarycentrics(rayQuery.CommittedTriangleBarycentrics());
        payload.PackInstanceGeometryIndex(rayQuery.CommittedInstanceIndex(), rayQuery.CommittedGeometryIndex());    
      
    }

#else // !USE_RAY_QUERY    
    TraceRay(Scene, RAY_FLAG_NONE, 0xFF, 0, 0, 0, ray, payload);
#endif
    
    float3 color = float3(0.0f, 0.0f, 0.0f);
    
    if (payload.Hit())
    {
        float3 uvw = GetBary(payload.Barycentrics());

        Instance instance;
        Mesh mesh = GetMesh(payload, instance);
        
        Vertex v0, v1, v2;
        GetVertices(mesh.GeometryIdx, payload.primitiveIndex, v0, v1, v2);       
        
        float3x3 objectToWorld3x3 = mul((float3x3) instance.Transform, (float3x3) mesh.Transform);
        
        float3 normalWS = normalize(mul(objectToWorld3x3, Interpolate(v0.Normal, v1.Normal, v2.Normal, uvw)));
        
        color = normalWS * 0.5f + 0.5f;
    }
    
    Output[idx] = float4(color, 1.0f);
}